// Save the current stack pointer to user_sp
alias user_sp R1;
user_sp = SP;

// Get the current process ID and user ID from the system status table
alias pid R2;
pid = [SYSTEM_STATUS_TABLE + 1];
alias user_id R3;
user_id = [SYSTEM_STATUS_TABLE];

// Set the mode flag in the process table to indicate shutdown
[PROCESS_TABLE + 16*pid + 9] = 21;
// Save the user stack pointer in the process table
[PROCESS_TABLE + 16*pid + 13] = user_sp;

// Switch to the kernel stack
SP = [PROCESS_TABLE + 16*pid + 11]*512 - 1;

// Check if the current process is the shell and the user is root
if(pid != 2 || user_id != 1) then
    // If not, return -1 and reset the mode flag
    [[PTBR + 2*(user_sp-1)/512]*512 + (user_sp-1)%512] = -1;
    [PROCESS_TABLE + 16*pid + 9] = 0;
    SP = user_sp;
    ireturn;
endif;

// Kill all user processes except idle, login, and shell
multipush(R1, R2, R3);
R1 = 5;
R2 = pid;
call PROCESS_MANAGER;
multipop(R1, R2, R3);

// Commit dirty buffers to disk
R4 = 0;
while(R4 < MAX_BUFFER) do
    if([BUFFER_TABLE + 4*R4 + 1] == 1) then
        multipush(R1, R2, R3, R4);
        R1 = DISK_STORE;
        R2 = pid;
        R3 = BUFFER_BASE + R4; 
        R4 = [BUFFER_TABLE + 4*R4]; 
        call DEVICE_MANAGER;
        multipop(R1, R2, R3, R4);
    endif;
    
    R4 = R4 + 1;
endwhile;

// Store the inode table and user table to disk
multipush(R1, R2, R3);
R1 = 1;
R2 = pid; 
R3 = 59;
R4 = 3; 
call DEVICE_MANAGER;
multipop(R1, R2, R3);

multipush(R1, R2, R3);
R1 = 1;
R2 = pid;
R3 = 60; 
R4 = 4; 
call DEVICE_MANAGER;
multipop(R1, R2, R3);

// Store the root file to disk
multipush(R1, R2, R3);
R1 = 1;
R2 = pid;
R3 = 62;
R4 = 5;
call DEVICE_MANAGER;
multipop(R1, R2, R3);

// Store the disk free list to disk
multipush(R1, R2, R3);
R1 = 1;
R2 = pid; 
R3 = 61;
R4 = 2; 
call DEVICE_MANAGER;
multipop(R1, R2, R3);

// Halt the system
halt;